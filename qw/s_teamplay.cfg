// BUGS:

// TODO:
// Smart button more?
// alias __dm3      
// 2on2 special cfg

// LEDs:
// $x86 = green   - teammates, safe, u take, has, our, took, coming, camping, ok, dont kill, i take, my pack, soon, unlocked, powerup over, rl died 
// $x87 = red     - enemies, enemy powerups, death messages, negative, nmy coming from, nmy holds, breached, enemy rl, discharge warning, nmy took secret ra, lava opened, lag report
// $x89 = blue    - pointed items that are not: powerups (except the biosuit), eyes, enemies or teammates 
// $x8b = brown   - chat, questions, pointed eyes, orders without specified location
// $x88 = yellow  - ...everything else

// Magic LEDs:
// $ledpoint      - the type of object you point
// $ledstatus     - related to how much do you need

////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                                    TEAMPLAY LITE                                               //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////
alias _.do.nothing      ""

//_.resetAll
alias _.resetAll "unalias_re ^\.tsay\.(.+)$; unalias_re ^\_\.(.+)$; unalias_re ^stp\_(.+)$; unset_re ^stp\_(.+)$; unset_re ^\_\.(.+)$"
alias _.echo.re "echo 0:$re_trigger_match_0 1:$re_trigger_match_1 2:$re_trigger_match_2"

// stp_ Style
////////////////////////////////////////////////////////////////////////////////////////////////////
alias _.stp.default.style "stp_setstyle 1"
alias stp_setstyle "set stpstyle %1; _.nick.extract; stp_init_echo"
alias stp_style "stp_setstyle %1"


// stp_ Version
////////////////////////////////////////////////////////////////////////////////////////////////////
set _.stp.version "0.8.0"
alias stp_init_echo "echo &cff0$x80Sudden Teamplay lite $_.stp.version (style: $stpstyle) loaded$x82&r"


// stp_ Set nick
////////////////////////////////////////////////////////////////////////////////////////////////////
unset_re ^stp_nick(.*)$
unset_re ^\\_\.str\.nick\.(.+)$
unalias_re ^\_\.nick[c]?\.(.+)$
set _.str.nick          ""

// set nick based on name
alias _.nick.name       "if_exists alias _.nick.$name _.nick.$name _.do.nothing"
alias _.nick.andeh      "set nick {&cf68and:&r}; skin andeh"
alias _.nick.carapace   "set nick {&c693car:&r}; skin carapace"
alias _.nick.bps        "set nick {&cc93bps:&r}; skin bps"
alias _.nick.ganon      "set nick {&c48fgan:&r}; skin ganon"
alias _.nick.goblin     "set nick {&c859gob:&r}; skin goblin"
alias _.nick.lethalwiz  "set nick {&c358lwz:&r}; skin lethalwiz"
alias _.nick.molle      "set nick {&c962mol:&r}; skin molle"
alias _.nick.razor      "set nick {&ccc6raz:&r}; skin razor"
alias _.nick.rio        "set nick {&c823rio:&r}; skin rio"
alias _.nick.rst        "set nick {&c777rst:&r}; skin rst"
_.nick.name

// Check if nick has color in it
set stp_nick_rgb ""
set stp_nick_pure ""
alias _.nick.color.get  "set stp_nick_rgb $qt$qt; if ('$nick' =~ '&c([0-9a-f]{3})') then _.nick.color.get.m else _.nick.color.get.d"
alias _.nick.color.get.m "set stp_nick_rgb $re_trigger_match_1"
alias _.nick.color.get.d "set stp_nick_rgb 555"
alias _.nick.pure.get "set stp_nick_pure $qt$qt; if ('$nick' =~ '(&c[0-9a-f]{3})?([a-zA-Z0-9]{3})\:?') then _.nick.pure.get.m"
alias _.nick.pure.get.m "set stp_nick_pure $re_trigger_match_2"

alias _.nick.extract "_.nick.color.get; _.nick.pure.get; _.nick.set"
// stp_style 1, 0 uses nik: while 2 has no colon
alias _.nick.set "if ($stpstyle == 1) then set nick {&c$stp_nick_rgb$stp_nick_pure:&r} else set nick {&c$stp_nick_rgb$stp_nick_pure&r}; if ($stpstyle == 2) then set _.str.nick $stp_nick_pure"

if_exists cvar stpstyle _.do.nothing _.stp.default.style


// tp_ Settings
////////////////////////////////////////////////////////////////////////////////////////////////////
// Item can be: quad, pent, ring, suit, ra, ya, ga, mh, health, lg, rl, gl, sng, ng, ssg, pack, cells, rockets, nails, shells, flag, teammate, enemy, eyes, sentry, disp, runes
tp_pointpriorities 0
tp_pickup powerups rl lg pack armor mh ssg sng gl cells
tp_point powerups ra rl lg pack ya ga enemy mh ssg sng gl rockets cells 
tp_took powerups rl lg pack armor mh ssg sng gl cells rockets

tp_need_weapon	    "87"
tp_need_health	    "50"
tp_need_ga		    "70"
tp_need_ya		    "70"
tp_need_ra		    "70"
tp_need_shells	    "0"
tp_need_nails	    "0"
tp_need_cells	    "6"
tp_need_rockets     "6"


// tp_ Names
////////////////////////////////////////////////////////////////////////////////////////////////////
tp_name_teammate            "" // shows name of player
tp_name_enemy               "{&cf55enemy&cfff}"
tp_name_gl                  "{&c299gl&cfff}"
tp_name_rlg                 "{&cf31r&cfff}{&c29flg&cfff}"
tp_name_rl                  "{&cf31rl&cfff}"
tp_name_lg                  "{&c29flg&cfff}"
tp_name_ga                  "{&c0b0ga&cfff}"
tp_name_ya                  "{&cff0ya&cfff}"
tp_name_ra                  "{&ce00ra&cfff}"
tp_name_armortype_ga        "{&c693g&cfff}"
tp_name_armortype_ya        "{&c991y&cfff}"
tp_name_armortype_ra        "{&c911r&cfff}"
tp_name_rockets             "rox"
tp_name_cells               "cells"
tp_name_quad                "{&c05fquad&cfff}"
tp_name_pent                "{&cf00pent&cfff}"
tp_name_ring                "{&cff0ring&cfff}"
tp_name_mh                  "{&c0ffmega&cfff}"
set_tp tp_name_mega         "{&c0ffmega&cfff}"
tp_name_backpack            "{&cf2apack&r}"
tp_name_eyes                "{&cff0eyes&cfff}"
tp_name_quaded              "{&c05fquaded&cfff}"
tp_name_pented              "{&cf00pented&cfff}"
tp_name_someplace           " "
tp_name_at                  "{&c999at&r}"
tp_name_nothing             " "
tp_name_none                ""
tp_name_separator           "$x20"


// Colors
////////////////////////////////////////////////////////////////////////////////////////////////////
set_tp _.w                  "&cfff"
set_tp _.w2                 "&caaa"
set_tp _.w3                 "&c666"
set_tp _.r                  "&cf00"
set_tp _.r2                 "&cf55"
set_tp _.r3                 "&cf99"
set_tp _.g                  "&c0f0"
set_tp _.g2                 "&c5f5"
set_tp _.g3                 "&c9f9"
set_tp _.b                  "&c05f"
set_tp _.b2                 "&c66f"
set_tp _.b3                 "&c99f"
set_tp _.m                  "&cf0f"
set_tp _.m2                 "&cf5f"
set_tp _.m3                 "&cf9f"
set_tp _.c                  "&c0ff"
set_tp _.c2                 "&c5ff"
set_tp _.c3                 "&c9ff"
set_tp _.y                  "&cff0"  
set_tp _.y2                 "&cff5"
set_tp _.y3                 "&cff9"
set_tp _.u                  "&cfa1"
set_tp _.u2                 "&cfa6"
set_tp _.u3                 "&cfa9"

alias set_color_tp          "set_tp {%1%2$r}"


// Variables
////////////////////////////////////////////////////////////////////////////////////////////////////
set_tp stp_name_q           "{&c05fq&cfff}"
set_tp stp_name_p           "{&cf00p&cfff}"
set_tp stp_name_r           "r"
set_tp stp_name_rl          "rl"
set_tp stp_name_lg          "lg"

set_tp _.str.weapon         ""
set_tp _.str.health         ""
set_tp _.str.armor          ""
set_tp _.str.powerups       ""
set_tp _.str.took           ""
set_tp _.str.killme         ""
set_tp _.str.need           ""

set_tp _.isPowerful 0


// Aliases
////////////////////////////////////////////////////////////////////////////////////////////////////
alias app_tp                "if ('$%1' == '') then set_tp %1 $%1%2 else set_tp %1 $%1$x20%2"
alias app_tp_nosp           "set_tp %1 $%1%2"  

// checks - check attributes and set strings with data
alias _.check.all          "_.check.power; _.check.stack; _.check.powerups; _.check.armor; _.check.health; _.check.weapons; _.check.need"

alias _.check.power         "if ('$bestweapon' isin '$tp_name_rl $tp_name_lg' OR '$powerups' != '') then set_tp powerful 1 else set_tp powerful 0; wait"
alias _.check.stack         "if (($armor > 50 AND $health > 100) OR ($armor >= 100)) then set_tp stacked 1 else set_tp stacked 0; wait"

alias _.check.need          "set_tp _.str.need $qt$qt; if ('$matchtype' == '2on2') then _.need.2on2 else _.need.4on4; wait; _.need.any"
alias _.need.2on2           "_.need.armor; _.need.health;"
alias _.need.4on4           "_.need.armor; _.need.health; _.need.weapon"
alias _.need.armor          "if ('$tp_name_weapon' !isin '$need' AND '$tp_name_armor' isin '$need' AND $armor < 999) then app_tp _.str.need $tp_name_armor else _.do.nothing"
alias _.need.health         "if ('$tp_name_weapon' !isin '$need' AND '$tp_name_health' isin '$need') then app_tp _.str.need $tp_name_health else _.do.nothing"
alias _.need.ammo           "if (('$tp_name_rl' isin '$weapons' AND '$tp_name_rockets' isin '$need') OR ('$tp_name_lg' isin '$weapons' AND $cells <= $tp_need_cells)) then app_tp _.str.need ammo else _.do.nothing"
alias _.need.weapon         "if (($powerful > 0 OR $stacked > 0) AND '$bestweapon' !isin '$tp_name_rl $tp_name_lg') then app_tp _.str.need weapon else _.do.nothing"
alias _.need.any            "if ('$_.str.need' != '') then set_tp _.str.need need$x20$_.str.need"

alias _.check.powerups      "set_tp _.str.powerups $qt$qt; if ('$powerups' = '') then _.do.nothing else _.pu"
alias _.pu                  "if ('$tp_name_quad' isin '$powerups' AND '$tp_name_pent' isin '$powerups' AND '$tp_name_ring' isin '$powerups') then _.pu.all else if ('$tp_name_quad' isin '$powerups' AND '$tp_name_pent' isin '$powerups') then _.pu.quadpent else if ('$tp_name_quad' isin '$powerups' AND '$tp_name_ring' isin '$powerups') then _.pu.quadring else if ('$tp_name_ring' isin '$powerups' AND '$tp_name_pent' isin '$powerups') then _.pu.pentring else _.pu.single"
alias _.pu.all              "set_tp _.str.powerups $stp_name_q$stp_name_p$tp_name_ring"
alias _.pu.quadpent         "set_tp _.str.powerups $stp_name_q$tp_name_pent"
alias _.pu.quadring         "set_tp _.str.powerups $stp_name_q$tp_name_ring"
alias _.pu.pentring         "set_tp _.str.powerups $stp_name_p$tp_name_ring"
alias _.pu.single           "set_tp _.str.powerups $powerups"

alias _.check.weapons       "set_tp _.str.weapon $qt $qt; set_tp _.str.rlg $qt$qt; _.check.rlg; if ('$tp_name_rl' isin '$weapons' AND '$tp_name_lg' isin '$weapons') then _.has.weapon.both   else if ('$tp_name_rl' isin '$weapons') then _.has.weapon.rl else if ('$tp_name_lg' isin '$weapons') then _.has.weapon.lg else _.has.weapon.weak"
alias _.has.weapon.both     "_.has.weapon.rl; _.has.weapon.lg"

alias _.has.weapon.rl       "app_tp _.str.weapon $tp_name_rl; _.has.weapon.rl.low"
alias _.has.weapon.rl.low   "if ($rockets <= $tp_need_rockets) then app_tp_nosp _.str.weapon {&cf66:$rockets&r} else app_tp_nosp _.str.weapon {&cf31:$rockets&cfff}"
alias _.has.weapon.lg       "app_tp _.str.weapon $tp_name_lg; _.has.weapon.lg.low"
alias _.has.weapon.lg.low   "if ($cells <= $tp_need_cells) then app_tp_nosp _.str.weapon {&cf66:$cells&r} else app_tp_nosp _.str.weapon {&c29f:$cells&r}"
alias _.has.weapon.weak     "if ('$bestweapon' isin 'sg ng') then _.has.weapon.no else app_tp _.str.weapon $bestweapon"
alias _.has.weapon.no       "set_tp _.str.weapon $qt$qt"
alias _.check.rlg           "set_tp _.str.rlg $qt$qt; set_tp _.str.rlgc $qt$qt; if ('$tp_name_rl' isin '$weapons' AND '$tp_name_lg' isin '$weapons') then _.has.rlg.both else if ('$tp_name_rl' isin '$weapons') then _.has.rlg.rl else if ('$tp_name_lg' isin '$weapons') then _.has.rlg.lg"
alias _.has.rlg.both        "set_tp _.str.rlg rlg; set_tp _.str.rlgc $tp_name_rlg"
alias _.has.rlg.rl          "set_tp _.str.rlg rl; set_tp _.str.rlgc $tp_name_rl"
alias _.has.rlg.lg          "set_tp _.str.rlg lg; set_tp _.str.rlgc $tp_name_lg"

alias _.check.health        "set_tp _.str.health $qt$qt; if ('$tp_name_health' isin '$need') then _.health.need else if ($health > 100) then _.health.stacked else _.health.normal"
alias _.health.need         "if ($_.isPowerful) then app_tp _.str.health {&cf66%h&r} else _.health.normal"
//alias _.health.stacked    "app_tp _.str.health {&cafa%h&r}"
alias _.health.stacked      "_.health.normal"
alias _.health.normal       "app_tp _.str.health {&cfff%h&r}"

alias _.check.armor         "set_tp _.str.armor $qt$qt; if ('$tp_name_armor' isin '$need') then _.armor.need else _.armor.normal"
alias _.armor.need          "if ($_.isPowerful) then app_tp _.str.armor %A{&cf66%a&r} else _.armor.normal"
alias _.armor.normal        "app_tp _.str.armor %A{&cfff%a&r}"

// report
alias _.report              "_.check.all"

// point
alias _.point               "if_exists alias shownick _.shownick1 _.do.nothing; if ('$point' != '$tp_name_nothing') then .tsay.point"
alias _.shownick1           "shownick 1"

// took
alias _.took                "if ('$took' == '$tp_name_nothing') then _.do.nothing else if ('$took' isin '$tp_name_ring $tp_name_pent $tp_name_quad') then _.team.powerup else if ('$took' isin '$tp_name_backpack $bestweapon' AND $health < 1) then _.took.died else if ('$took' isin '$tp_name_rl $tp_name_lg' AND '$bestweapon' !isin '$tp_name_rl $tp_name_lg') then _.took.died else if ('$tp_name_backpack' == '$took') then _.took.pack else _.took.any"
alias _.took.pack           "if ('$bestweapon' isin '$tp_name_rl $tp_name_lg') then _.took.any else .tsay.took.nopack"
alias _.took.died           ".tsay.took.died"
alias _.took.any            ".tsay.took.any"

// team quad/ring/pent
alias _.team.powerup        "if ('$powerups' == '') then .tsay.quad.over else .tsay.team.powerup"

// kill me
alias _.killme              "set_tp _.str.killme $qt$qt; if ('$bestweapon' == '$tp_name_rl') then _.killme.rl else if ('$bestweapon' == '$tp_name_lg') then _.killme.lg else _.killme.ammo"
alias _.killme.rl           "app_tp _.str.killme $tp_name_rl{&cf31:$rockets&r} else _.do.nothing"
alias _.killme.lg           "app_tp _.str.killme $tp_name_lg{&c29f:$cells&r} else _.do.nothing"
alias _.killme.ammo         "if ($rockets < 1 AND $cells < 1) then _.do.nothing else if ($rockets > 0) then app_tp _.str.killme {&cfa6$tp_name_rockets:$rockets&r} else _.do.nothing; if ($cells > 0) then app_tp _.str.killme {&c6af$tp_name_cells:$cells&r} else _.do.nothing"

// lost
alias _.lost                "set_tp _.str.dropped $qt$qt; if ($health < 1 AND '$weapon' == '$tp_name_rl') then _.lost.rl else if ($health < 1 AND '$weapon' == '$tp_name_lg') then _.lost.lg else _.lost.nothing"
alias _.lost.rl             "app_tp _.str.dropped {&cf31rl-pack&r}; wait; .tsay.lost.weapon"
//alias _.lost.rl             "app_tp _.str.dropped {$_.mrl-pack&r}; wait; .tsay.lost.weapon"
alias _.lost.lg             "app_tp _.str.dropped {&c29flg-pack:$cells&r}; wait; .tsay.lost.weapon"
//alias _.lost.lg             "app_tp _.str.dropped {$_.mlg-pack:$cells&r}; wait; .tsay.lost.weapon"
alias _.lost.nothing        "app_tp _.str.dropped {$_.r2%E&r}; wait; .tsay.lost"
alias _.lost.pack           "if ($health < 1 AND '$bestweapon' isin '$tp_name_rl $tp_name_lg') then _.lost else .tsay.lost.pack"

// you take - transform this item location to something more generic?
alias _.youtake             "set_tp _.str.youtake {$_.w2x&r}; if ('$point' != '$tp_name_nothing') then _.youtake.point else _.youtake.regxp"
alias _.youtake.point       "set_tp _.str.youtake $point"  
alias _.youtake.regxp       "if ('$location' =~ '^(quad|pent|ring|rl|lg|ra|ya|mega)$') then _.youtake.item else if ('$location' =~ '-?(quad|pent|ring|rl|lg|ra|ya|mega)-?') then _.youtake.item else if ('$mapname' == 'dm3') then _.youtake.dm3 else if ('$mapname' == 'dm2') then _.youtake.dm2 else if ('$mapname' == 'e1m2') then _.youtake.e1m2"
alias _.youtake.item        "_.youtake.tp $re_trigger_match_1"
alias _.youtake.dm3         "if ('$location' =~ '^(sng\-ledge|hill)$') then _.youtake.tp mega else if ('$location' =~ '^(water)$') then _.youtake.tp lg"
alias _.youtake.dm2         "if ('$location' =~ '^(low\-stairs|low\-lift|low\-button)$') then _.youtake.tp rl else if ('$location' =~ '^(floating)$') then _.youtake.tp mh"
alias _.youtake.e1m2        "if ('$location' =~ '^([^\-]+)\-([^\-]+)$') then _.youtake.item2 else if ('$location' =~ '^(bridge|bridge\-room)$') then _.youtake.tp ga"
alias _.youtake.tp          "set_tp _.str.youtake $tp_name_%1"

// CHECK IF_EXISTS TP_NAME_R1 eller R2, skulle kunna ersätta location check i regxp, location är bättre för man kanske inte vill säga SNG
//alias _.youtake.regxp2       "if ('$location' =~ '^([^\-]+)$') then _.youtake.item2 else if ('$location' =~ '^([^\-]+)\-([^\-]+)$') then _.youtake.item2"
//alias _.youtake.item2       "_.youtake.item2.1 $re_trigger_match_1 $re_trigger_match_2"         
//alias _.youtake.item2.1     "echo 1:%1; if ('$' !isin '$tp_name_%1') then _.youtake.tp %1 else if ('$' !isin '$tp_name_%2') then _.youtake.tp %2 else _.youtake.tp at"

// smart: trick/slip/coming
alias _.smart               "if ('$mapname' == 'dm3') then _.smart.dm3 else if ('$mapname' == 'dm2') then _.smart.dm2 else if ('$mapname' == 'e1m2') then _.smart.e1m2 else stp_coming"
alias _.smart.dm3           "if ('$location' isin 'water water-lg water-gl water-rox water-tunnel' AND '$tp_name_lg' isin '$weapons') then .tsay.dm3.diss else stp_coming"
alias _.smart.dm2           "if ('$location' isin 'quad quad-low') then .tsay.dm2.trick else if ('$location' isin 'ng tele-entry tele') then .tsay.dm2.slipped else if ('$location' == 'big-stairs') then .tsay.dm2.rjump else if ('$location' == 'secret') then .tsay.dm2.secret else .tsay.coming"
alias _.smart.e1m2          "stp_coming"

// STP_STYLE - Alias routing
////////////////////////////////////////////////////////////////////////////////////////////////////
alias .tsay.version         "say_team $\$nick {$_.ystp version: $_.w$_.stp.version (style: $stpstyle)&r}"
alias .tsay.report          ".tsay$stpstyle.report"
alias .tsay.point           ".tsay$stpstyle.point"

alias .tsay.took.any        ".tsay$stpstyle.took.any"
alias .tsay.took.nopack     ".tsay$stpstyle.took.nopack"
alias .tsay.took.died       ".tsay$stpstyle.took.died"
alias .tsay.you.take        ".tsay$stpstyle.you.take"

alias .tsay.quad.over       ".tsay$stpstyle.quad.over"
alias .tsay.team.powerup    ".tsay$stpstyle.team.powerup"

alias .tsay.enemy.quad      ".tsay$stpstyle.enemy.quad"
alias .tsay.enemy.pent      ".tsay$stpstyle.enemy.pent"
alias .tsay.enemy.ring      ".tsay$stpstyle.enemy.ring"

alias .tsay.get.quad        ".tsay$stpstyle.get.quad"
alias .tsay.get.pent        ".tsay$stpstyle.get.pent"

alias .tsay.killme          ".tsay$stpstyle.killme"

alias .tsay.lost            ".tsay$stpstyle.lost"
alias .tsay.lost.weapon     ".tsay$stpstyle.lost.weapon"
alias .tsay.lost.pack       ".tsay$stpstyle.lost.pack"
alias .tsay.safe            ".tsay$stpstyle.safe"
alias .tsay.help            ".tsay$stpstyle.help"
alias .tsay.replace         ".tsay$stpstyle.replace"
alias .tsay.coming          ".tsay$stpstyle.coming"

alias .tsay.dm2.trick       ".tsay$stpstyle.dm2.trick"
alias .tsay.dm2.slipped     ".tsay$stpstyle.dm2.slipped"
alias .tsay.dm2.rjump       ".tsay$stpstyle.dm2.rjump"
alias .tsay.dm2.secret      ".tsay$stpstyle.dm2.secret"
alias .tsay.dm3.diss        ".tsay$stpstyle.dm3.diss"

alias .tsay.weapon.killed   ".tsay$stpstyle.weapon.killed"

// STP_STYLE 1 - sd_tac improved
// Colon efter nick:
// Rl visar rockets alltid
// Dropped rl (inte rl-pack)
//
// Imrovements?
// ------------
// Point item, quad over, enemy quad behöver inte visa ditt nick?
// ta bort nick ifrån enemy quad
// kill me visar hp och vad man har?
// lost pack visar vilken typ av pack
// Behöver verkligen nick: följas utav colon, eller är det bara fult?
// Visa RL/LG på Safe
// Visa armor/hp på Help
////////////////////////////////////////////////////////////////////////////////////////////////////
alias .tsay1.report         "say_team $\$nick $_.str.armor/$_.str.health$_.str.weapon $[{%l}$] $_.str.powerups $_.str.need"
alias .tsay1.point          "say_team $\$nick %x $tp_name_at $[{%y}$]"

alias .tsay1.took.any       "say_team $\$nick took $took $[{%Y}$]"
alias .tsay1.took.nopack    "say_team $\$nick took empty pack $[{%Y}$]"
alias .tsay1.took.died      "say_team $\$nick took $took but died $[{%Y}$]"
alias .tsay1.you.take       "say_team $\$nick you take $_.str.youtake $[{%l}$]"

alias .tsay1.quad.over       "say_team $\$nick $tp_name_quad over"
alias .tsay1.team.powerup    "say_team $\$nick {$_.gteam&r} $powerups"

alias .tsay1.enemy.quad      "say_team $\$nick $tp_name_enemy $tp_name_quad"
alias .tsay1.enemy.pent      "say_team $\$nick $tp_name_enemy $tp_name_pent"
alias .tsay1.enemy.ring      "say_team $\$nick $tp_name_enemy $tp_name_ring"

alias .tsay1.get.quad        "say_team $\$nick get $tp_name_quad"
alias .tsay1.get.pent        "say_team $\$nick get $tp_name_pent"

alias .tsay1.killme          "say_team $\$nick {$_.mkill me&r} $[{%l}$] $_.str.killme"

alias .tsay1.lost            "say_team $\$nick {$_.rlost&r} $[{%d}$] {%E}"
alias .tsay1.lost.weapon     "say_team $\$nick {$_.rdropped&r} $_.str.dropped $[{%d}$] {%E}"
alias .tsay1.lost.pack       "say_team $\$nick {$_.rdropped&r} $tp_name_backpack $[{%d}$] {%E}"
alias .tsay1.safe            "say_team $\$nick {$_.gsafe&r} $[{%l}$]"
alias .tsay1.help            "say_team $\$nick {$_.yhelp&r} $[{%l}$]"
alias .tsay1.replace         "say_team $\$nick replace $[{%l}$]"
alias .tsay1.coming          "say_team $\$nick coming $[{%l}$] $_.str.need"

alias .tsay1.dm2.trick       "say_team $\$nick quad-jump $[{quad-low}$]"
alias .tsay1.dm2.slipped     "say_team $\$nick {$_.menemy slipped&r} $[{tele}$]"
alias .tsay1.dm2.rjump       "say_team $\$nick coming rjump high $[{%l}$]"
alias .tsay1.dm2.secret      "say_team $\$nick do not enter $[{%l}$] %A%a/%h $_.str.rlg"
alias .tsay1.dm3.diss        "say_team $\$nick discharge warning! $[{%l}$] {&c29flg:$cells&r}"

alias .tsay1.weapon.killed   "say_team $\$nick {$_.gweapon killed!&r} $[{%l}$] {\}{&c0ff0&cfff}{/}"


// STP_STYLE 2 - Experimental color/layout
////////////////////////////////////////////////////////////////////////////////////////////////////
alias .tsay2.report          "say_team $\$nick $_.str.armor/$_.str.health$_.str.weapon $[{%l}$] $_.str.powerups {$_.w2$_.str.need}"
alias .tsay2.point           "say_team $\{&c1ff$d&r} %x $tp_name_at $[{%y}$]"

alias .tsay2.took.any        "say_team $\{$_.w$_.str.nick took&r} $took {$_.w3[$_.w2%Y$_.w3]&r}"
alias .tsay2.took.nopack     "say_team $\{$_.w$_.str.nick took $_.w2empty pack $_.w2[$_.w3%Y$_.w2]&r}"
alias .tsay2.took.died       "say_team $\{$_.w$_.str.nick took&r} $took {$_.w2but died&r}"
alias .tsay2.you.take        "say_team $\{$_.w$_.str.nick $_.wyou take $_.c$d&r} $_.str.youtake {$_.w3[$_.w%l$_.w3]&r}"

alias .tsay2.quad.over       "say_team $\$tp_name_quad {$_.bover&r}"
alias .tsay2.team.powerup    "say_team $\$nick $powerups"

alias .tsay2.enemy.quad      "say_team $\{$_.r2enemy&r} $tp_name_quad"
alias .tsay2.enemy.pent      "say_team $\{$_.r2enemy&r} $tp_name_pent"
alias .tsay2.enemy.ring      "say_team $\{$_.r2enemy&r} $tp_name_ring"

alias .tsay2.get.quad        "say_team $\{$_.b3get&r} $tp_name_quad"
alias .tsay2.get.pent        "say_team $\{$_.r3get&r} $tp_name_pent"

alias .tsay2.killme          "say_team $\$nick {$_.m2%hhp $_.mkill me!!&r} $_.str.killme $[{%l}$]"

alias .tsay2.lost            "say_team $\{$_.r$_.str.nick lost&r} {$_.r[$_.r2%d$_.r]&r} {$_.r2%E&r}"
alias .tsay2.lost.weapon     "say_team $\{$_.r$_.str.nick dropped&r} $_.str.dropped {$_.r[$_.w%d$_.r]&r} {$_.r2%E&r}"
alias .tsay2.lost.pack       "say_team $\{$_.r$_.str.nick dropped&r} $tp_name_backpack {$_.r[$_.w%d$_.r]&r} {$_.r2%E&r}"
alias .tsay2.safe            "say_team $\{$_.g$_.str.nick safe $_.g[$_.g2%l$_.g]&r} {$_.g3$_.str.rlg&r}"
alias .tsay2.help            "say_team $\{$_.y$_.str.nick help $_.y[$_.y2%l$_.y]&r} {$_.y3%a/%h&r}"
alias .tsay2.replace         "say_team $\{$_.c$_.str.nick replace $_.c[$_.c2%l$_.c]&r} {$_.c3$_.str.rlg&r}"
alias .tsay2.coming          "say_team $\{$_.u$_.str.nick&r} {$_.ucoming $_.u2[$_.u3%l$_.u2]&r} {$_.w2$_.str.need}"

alias .tsay2.dm2.trick       "say_team $\{$_.c$_.str.nick trick $_.c2[$_.c3%l$_.c2]&r}"
alias .tsay2.dm2.slipped     "say_team $\{$_.r2enemy slipped&r}"
alias .tsay2.dm2.rjump       "say_team $\{$_.u$_.str.nick coming rjump high $_.u2[$_.u3%l$_.u2]&r}"
alias .tsay2.dm2.secret      "say_team $\{$_.u$_.str.nick do not enter $_.u2[$_.u3%l$_.u2]&r} %A{$_.u2%a/%h&r} $_.str.rlg"
alias .tsay2.dm3.diss        "say_team $\{$_.b$_.str.nick discharge warning! $_.b2[$_.b3%l$_.b2]&r} {&c29flg:$cells&r}"

alias .tsay2.weapon.killed   "say_team $\{$_.g$_.str.nickn weapon killed! $_.g2[$_.g3%l$_.g2]&r} {\}{&c0ff0&cfff}{/}"

alias echo_tecken50         "echo 50:$x50 51:$x51 52:$x52 53:$x53 54:$x54 55:$x55 56:$x56 57:$x57 58:$x58 59:$x59"
alias echo_tecken60         "echo 60:$x60 61:$x61 62:$x62 63:$x63 64:$x64 65:$x65 66:$x66 67:$x67 68:$x68 69:$x69"
alias echo_tecken70         "echo 70:$x70 71:$x71 72:$x72 73:$x73 74:$x74 75:$x75 76:$x76 77:$x77 78:$x78 79:$x79"
alias echo_tecken80         "echo 80:$x80 81:$x81 82:$x82 83:$x83 84:$x84 85:$x85 86:$x86 87:$x87 88:$x88 89:$x89"
alias echo_tecken90         "echo 90:$x90 91:$x91 92:$x92 93:$x93 94:$x94 95:$x95 96:$x96 97:$x97 98:$x98 99:$x99"
alias echo_tecken           "echo_tecken50; echo_tecken60; echo_tecken70; echo_tecken80; echo_tecken90"


// Bind Aliases
////////////////////////////////////////////////////////////////////////////////////////////////////
alias stp_aol               "if ($health > 0) then %1 %2 %3 %4 %5 else stp_lost"
alias stp_spec              "if ('$spectator' == '1') then echo $_.wSudden teamplay message supressed when spectator = 1. else %1 %2 %3 %4 %5"

// Templay msg
alias stp_version           ".tsay.version"
alias stp_report            "_.report; wait; .tsay.report"
alias stp_point             "_.point"
alias stp_took              "_.took"
alias stp_killme            "impulse 7 8 6 5 4 3 2; _.check.health; wait; _.killme; wait; .tsay.killme"
alias stp_lost              "_.lost"
alias stp_dropped           "_.lost.pack"
alias stp_safe              "_.check.weapons; wait; .tsay.safe"
alias stp_powerup           "_.team.powerup"
alias stp_smart             "_.check.all; wait; _.smart"

alias stp_help              ".tsay.help"
alias stp_replace           "_.check.weapons; wait; .tsay.replace"
alias stp_coming            "_.check.all; wait; .tsay.coming"
alias stp_wpkilled          ".tsay.weapon.killed"
alias stp_youtake           "_.youtake; wait; .tsay.you.take"

alias stp_enemy_quad        ".tsay.enemy.quad"
alias stp_enemy_quad_ok98   "play ok98_enemy_quad; wait; .tsay.enemy.quad"
alias stp_enemy_pent        ".tsay.enemy.pent"
alias stp_enemy_ring        ".tsay.enemy.ring"
alias stp_get_quad          ".tsay.get.quad"
alias stp_get_pent          ".tsay.get.pent"