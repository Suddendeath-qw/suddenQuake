// I fallande importans:
// + report
// + point
// + took
// + enemy q/p/r
// + trick/slip 
// + killme
// - safe/lost
// - get quad/pent
// - help
// - replace
// - need
// - coming


// TODO:
// took but died
// Smart button more?
// alias __dm3      
// 2on2 special cfg

////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                                    TEAMPLAY LITE                                               //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

// LEDs:
// $x86 = green   - teammates, safe, u take, has, our, took, coming, camping, ok, dont kill, i take, my pack, soon, unlocked, powerup over, rl died 
// $x87 = red     - enemies, enemy powerups, death messages, negative, nmy coming from, nmy holds, breached, enemy rl, discharge warning, nmy took secret ra, lava opened, lag report
// $x89 = blue    - pointed items that are not: powerups (except the biosuit), eyes, enemies or teammates 
// $x8b = brown   - chat, questions, pointed eyes, orders without specified location
// $x88 = yellow  - ...everything else

// Magic LEDs:
// $ledpoint      - the type of object you point
// $ledstatus     - related to how much do you need

set _.stp.version "0.3.3"

// set nick
set stp_nick_color ""
alias _.extract_nick "set stp_nick $qt$qt; set stp_nickc $qt$qt; if ('$nick' =~ '{(&c[0-9a-f]{3})(.{3})') then _.extract_nick_set1b else _.extract_nick_set1a"
// if $nick is using no colors, use the nick but assign a default color
alias _.extract_nick_set1a "set stp_nick $nick; if_exists alias _.nickc.$nick _.nickc.$nick _.nickc.default"
alias _.nickc.rio "set stp_nick_color &c373; _.extract_nick_set2"
alias _.nickc.lwz "set stp_nick_color &c28a; _.extract_nick_set2"
alias _.nickc.gob "set stp_nick_color &ca33; _.extract_nick_set2"
alias _.nickc.mol "set stp_nick_color &ca82; _.extract_nick_set2"
alias _.nickc.and "set stp_nick_color &ca5a; _.extract_nick_set2"
alias _.nickc.car "set stp_nick_color &c5a5; _.extract_nick_set2"
alias _.nickc.bps "set stp_nick_color &ca82; _.extract_nick_set2"
alias _.nickc.default "set stp_nick_color &c555; _.extract_nick_set2"
// if $nick is using color coding, extract the color and the nick and set stp variables
alias _.extract_nick_set1b "set stp_nick_color $re_trigger_match_1; set stp_nick $re_trigger_match_2; _.extract_nick_set2"
alias _.extract_nick_set2 "set stp_nickc {$stp_nick_color$stp_nick&r}"
_.extract_nick

alias stp_version "echo $_.stp.version"
alias stp_version_say_team "say_team {&rSudden Teamplay lite $_.stp.version}"
alias stp_init_echo "echo &cff0$x80Sudden Teamplay lite $_.stp.version loaded$x82&r"
stp_init_echo

// tp_ Settings
////////////////////////////////////////////////////////////////////////////////////////////////////
// Item can be: quad, pent, ring, suit, ra, ya, ga, mh, health, lg, rl, gl, sng, ng, ssg, pack, cells, rockets, nails, shells, flag, teammate, enemy, eyes, sentry, disp, runes
tp_pointpriorities 0
tp_pickup powerups rl lg pack armor mh ssg sng gl cells
tp_point powerups ra rl lg pack ya ga enemy mh ssg sng gl rockets cells 
tp_took powerups rl lg pack armor mh ssg sng gl cells

tp_need_weapon			"87"
tp_need_health			"50"
tp_need_ga				"70"
tp_need_ya				"70"
tp_need_ra				"70"
tp_need_shells			"0"
tp_need_nails			"0"
tp_need_cells			"6"
tp_need_rockets         "5"


// tp_ Names
////////////////////////////////////////////////////////////////////////////////////////////////////
tp_name_teammate                      "" // shows name of player
tp_name_gl                            "{&c299gl&cfff}"
tp_name_rlg                           "{&cf13rl&cfff}{&c2aag&cfff}"
tp_name_rl                            "{&cf31rl&cfff}"
tp_name_lg                            "{&c29flg&cfff}"
tp_name_ga                            "{&c0b0ga&cfff}"
tp_name_ya                            "{&cff0ya&cfff}"
tp_name_ra                            "{&ce00ra&cfff}"
tp_name_armortype_ga                  "{&c693g&cfff}"
tp_name_armortype_ya                  "{&c991y&cfff}"
tp_name_armortype_ra                  "{&c931r&cfff}"
tp_name_rockets                       "rox"
tp_name_cells                         "cells"
tp_name_quad                          "{&c05fquad&cfff}"
tp_name_pent                          "{&cf00pent&cfff}"
tp_name_ring                          "{&cff0ring&cfff}"
tp_name_mh                            "{&c0ffmega&cfff}"
tp_name_backpack                      "{&cf2aPACK&r}"
tp_name_enemy                         "{&cfffenemy&cfff}"
tp_name_eyes                          "{&cff0eyes&cfff}"
tp_name_quaded                        "{&c05fquaded&cfff}"
tp_name_pented                        "{&cf00pented&cfff}"
tp_name_someplace                     " "
tp_name_at                            "at"
tp_name_nothing                       " "
tp_name_none                          ""
tp_name_separator                     "$x20"

// Colors
////////////////////////////////////////////////////////////////////////////////////////////////////
set_tp _.w        "&cfff"
set_tp _.w2       "&caaa"
set_tp _.w3       "&c666"
set_tp _.r        "&cf00"
set_tp _.r2       "&cf55"
set_tp _.r3       "&cf99"
set_tp _.g        "&c0f0"
set_tp _.g2       "&c5f5"
set_tp _.g3       "&c9f9"
set_tp _.b        "&c05f"
set_tp _.b2       "&c66f"
set_tp _.b3       "&c99f"
set_tp _.m        "&cf0f"
set_tp _.m2       "&cf5f"
set_tp _.m3       "&cf9f"
set_tp _.c        "&c0ff"
set_tp _.c2       "&c5ff"
set_tp _.c3       "&c9ff"
set_tp _.y        "&cff0"
set_tp _.y2       "&cff2"
set_tp _.y3       "&cff3"
set_tp _.u        "&cfa1"
set_tp _.u2       "&cfa6"
set_tp _.u3       "&cfa9"

set_tp stp_loc      "$[{%l}$]"
set_tp stp_locd     "$[%d$]"

alias set_color_tp "set_tp {%1%2$r}"


// Variables
////////////////////////////////////////////////////////////////////////////////////////////////////
set_tp stp_name_q                   "{&c05fq&cfff}"
set_tp stp_name_p                   "{&cf00p&cfff}"
set_tp stp_name_r                   "{&cff0r&cfff}"
set_tp stp_name_rl      "{&cf31rl&cfff}"
set_tp stp_name_lg      "{&c29fg&cfff}"

// report status
set_tp _.str.weapon               ""
set_tp _.str.health               ""
set_tp _.str.armor                ""
set_tp _.str.powerups             ""
set_tp _.str.took                 ""
set_tp _.str.killme               ""
set_tp _.str.need               ""

set_tp _.isPowerful 0


// Aliases
////////////////////////////////////////////////////////////////////////////////////////////////////
alias app_tp             "if ('$%1' == '') then set_tp %1 $%1%2 else set_tp %1 $%1$x20%2"
alias app_tp_nosp        "set_tp %1 $%1%2"  
alias _.do_nothing          ""
alias _.do.nothing          ""


// checks - check attributes and set strings with data
alias _.check_all           "_.check_power; _.check_powerups; _.check_armor; _.check_health; _.check_weapons; _.check_need"

alias _.check_power         "if ('$bestweapon' isin '$tp_name_rl $tp_name_lg' OR '$powerups' != '') then set_tp _.isPowerful 1 else set_tp _.isPowerful 0"  

alias _.check_need          "set_tp _.str.need $qt$qt; if ('$matchtype' == '2on2') then _.need_2on2 else _.need_4on4; wait; _.need_any"
alias _.need_2on2           "_.need_armor; _.need_health"
alias _.need_4on4           "_.need_armor; _.need_health"
alias _.need_armor          "if ('$tp_name_weapon' !isin '$need' AND '$tp_name_armor' isin '$need' AND $armor < 999) then app_tp _.str.need $tp_name_armor else _.do_nothing"
alias _.need_health         "if ('$tp_name_weapon' !isin '$need' AND '$tp_name_health' isin '$need') then app_tp _.str.need $tp_name_health else _.do_nothing"
alias _.need_any            "if ('$_.str.need' != '') then set_tp _.str.need need$x20$_.str.need"

alias _.check_powerups      "set_tp _.str.powerups $qt$qt; if ('$powerups' = '') then _.do_nothing else _.pu"
alias _.pu                  "if ('$tp_name_quad' isin '$powerups' AND '$tp_name_pent' isin '$powerups' AND '$tp_name_ring' isin '$powerups') then _.pu_all else if ('$tp_name_quad' isin '$powerups' AND '$tp_name_pent' isin '$powerups') then _.pu_quadpent else if ('$tp_name_quad' isin '$powerups' AND '$tp_name_ring' isin '$powerups') then _.pu_quadring else if ('$tp_name_ring' isin '$powerups' AND '$tp_name_pent' isin '$powerups') then _.pu_pentring else _.pu_single"
alias _.pu_all              "set_tp _.str.powerups $x20$stp_name_q$stp_name_p$tp_name_ring"
alias _.pu_quadpent         "set_tp _.str.powerups $x20$stp_name_q$tp_name_pent"
alias _.pu_quadring         "set_tp _.str.powerups $x20$stp_name_q$tp_name_ring"
alias _.pu_pentring         "set_tp _.str.powerups $x20$stp_name_p$tp_name_ring"
alias _.pu_single           "set_tp _.str.powerups $x20$powerups"

alias _.check_weapons       "set_tp _.str.weapon $qt$qt; set_tp _.str.rlg $qt$qt; if ('$tp_name_rl' isin '$weapons' AND '$tp_name_lg' isin '$weapons') then _.has_weapon_rlg else if ('$tp_name_rl' isin '$weapons') then _.has_weapon_rl else if ('$tp_name_lg' isin '$weapons') then _.has_weapon_lg else _.has_no_weapon"
alias _.has_weapon_rlg      "_.has_weapon_rl; _.has_weapon_lg"
alias _.has_weapon_rl       "app_tp _.str.weapon $tp_name_rl; _.has_weapon_rl_low; app_tp _.str.rlg $stp_name_rl"
alias _.has_weapon_rl_low   "if ('$tp_name_rockets' isin '$need') then app_tp_nosp _.str.weapon {&cf66:$rockets&r} else _.do_nothing"
alias _.has_weapon_lg       "app_tp _.str.weapon $tp_name_lg; _.has_weapon_lg_low; if ('$_.str.rlg' != '') then app_tp $stp_name_lg else app_tp _.str.rlg $tp_name_lg"
alias _.has_weapon_lg_low   "if ($cells < $tp_need_cells) then app_tp_nosp _.str.weapon {&cf66:$cells&r} else app_tp_nosp _.str.weapon {&c29f:$cells&r}"
alias _.has_no_weapon       "set_tp _.str.weapon $bestweapon"

alias _.check_health        "set_tp _.str.health $qt$qt; if ('$tp_name_health' isin '$need') then _.health_need else if ($health > 100) then _.health_stacked else _.health_normal"
alias _.health_need         "if ($_.isPowerful) then app_tp _.str.health {&cf66%h&r} else _.health_normal"
//alias _.health_stacked      "app_tp _.str.health {&cafa%h&r}"
alias _.health_stacked      "_.health_normal"
alias _.health_normal       "app_tp _.str.health {&cfff%h&r}"

alias _.check_armor         "set_tp _.str.armor $qt$qt; if ('$tp_name_armor' isin '$need') then _.armor_need else _.armor_normal"
alias _.armor_need          "if ($_.isPowerful) then app_tp _.str.armor %A{&cf66%a&r} else _.armor_normal"
alias _.armor_normal        "app_tp _.str.armor %A{&cfff%a&r}"

// report
alias _.report              "_.check_all"

// point
alias _.point               "if_exists alias shownick _.shownick1 _.do.nothing"
alias _.shownick1           "shownick 1"

// took
alias _.took                "if ('$took' isin '$tp_name_ring $tp_name_pent $tp_name_quad') then _.took_powerup else if ('$tp_name_backpack' isin '$took') then _.took.pack else _.took.any"
alias _.took_powerup        "if ('$tp_name_ring' == '$took') then _.took_ring else if ('$tp_name_quad' == '$took') then _.took_quad else if ('$tp_name_pent' == '$took') then _.took_pent else _.do_nothing"
alias _.took_ring           ".tsay.took.ring"
alias _.took_quad           "if ($health < 1 OR '$powerups' == '') then .tsay.quad.over else .tsay.took.quad"
alias _.took_pent           ".tsay.took.pent"
alias _.took.pack           "if ('$bestweapon' isin '' AND '$tp_name_rl $tp_name_lg') then _.took.any else .tsay.took.nopack"
alias _.took.any            ".tsay.took.any"

// team quad/ring/pent
alias _.team_powerup        "if ('$powerups' =='') then .tsay.quad.over else .tsay.team.powerup"

// kill me
alias _.killme              "impulse 7 8 6 5 3 4 2 1; set_tp _.str.killme $qt$qt; if ('$bestweapon' == '$tp_name_rl') then _.killme_rl else if ('$bestweapon' == '$tp_name_lg') then _.killme_lg else _.killme_ammo"
alias _.killme_rl           "app_tp _.str.killme $tp_name_rl{&cf31:$rockets&r} else _.do_nothing"
alias _.killme_lg           "app_tp _.str.killme $tp_name_lg{&c29f:$cells&r} else _.do_nothing"
alias _.killme_ammo         "if ($rockets < 1 AND $cells < 1) then _.do_nothing else if ($rockets > 0) then app_tp _.str.killme {&cfa6$tp_name_rockets:$rockets&r} else _.do_nothing; if ($cells > 0) then app_tp _.str.killme {&c6af$tp_name_cells:$cells&r} else _.do_nothing"

// lost
alias _.lost                "set_tp _.str.dropped $qt$qt; if ('$weapon' == '$tp_name_rl') then _.lost_rl else if ('$weapon' == '$tp_name_lg') then _.lost_lg else _.lost_nothing"
alias _.lost_rl             "app_tp _.str.dropped {$_.r(dropped$x20rl-pack)&r}"
alias _.lost_lg             "app_tp _.str.dropped {&c55f(dropped$x20lg-pack:$cells)&r}"
alias _.lost_nothing        "app_tp _.str.dropped {$_.r2%E&r}"

// smart: trick/slip/coming
alias _.smart               "if ('$mapname' == 'dm3') then _.smart_dm3 else if ('$mapname' == 'dm2') then _.smart_dm2 else if ('$mapname' == 'e1m2') then _.smart_e1m2 else stp_coming"
alias _.smart_dm3           "if ('$location' isin 'water water-lg water-gl water-rox water-tunnel' AND '$tp_name_lg' isin '$weapons') then .tsay.dm3.diss else stp_coming"
alias _.smart_dm2           "if ('$location' isin 'quad quad-low') then .tsay.dm2.trick else if ('$location' isin 'ng tele-entry tele') then .tsay.dm2.slipped else if ('$location' == 'big-stairs') then .tsay.dm2.rjump else if ('$location' == 'secret') then .tsay.dm2.secret else .tsay.coming"
alias _.smart_e1m2          "stp_coming"

// Output Alias
////////////////////////////////////////////////////////////////////////////////////////////////////
alias .tsay.report          "say_team $\  $stp_nickc$_.str.powerups $[{%l}$] $_.str.armor/$_.str.health $_.str.weapon {$_.w2$_.str.need}"
alias .tsay.point           "say_team $\{&c1ff$d&r} %x {&c777$tp_name_at&r} $[{%y}$]"

alias .tsay.took.any        "say_team $\{$_.w$stp_nick $_.w2[$_.w3%Y$_.w2] took&r} $took"
alias .tsay.took.ring       "say_team $\  {&cff1$stp_nick $x88$x88 team ring - dont shoot me&r} $x88$x88"
alias .tsay.took.quad       "say_team $\  {$_.b$stp_nick&r} $x89$x89 {$_.bteam quad typ&r} $x89$x89"
alias .tsay.took.pent       "say_team $\  {&cf11$stp_nick $x87$x87 team pent&r} $x87$x87"
alias .tsay.took.nopack     "say_team $\{$_.w$stp_nick $_.w2[$_.w3%Y$_.w2] took $_.w3no pack&r}"
alias .tsay.quad.over       "say_team $\  $tp_name_quad {&c00fover&r}"
alias .tsay.team.powerup    "say_team $\  $stp_nickc $powerups"

alias .tsay.enemy.quad      "say_team $\ enemy $tp_name_quad"
alias .tsay.enemy.pent      "say_team $\ enemy $tp_name_pent"
alias .tsay.enemy.ring      "say_team $\ enemy $tp_name_ring"

alias .tsay.killme          "say_team $\  $stp_nickc $[{%l}$] {$_.m2%hhp $_.mkill me!!&r} $_.str.killme"

alias .tsay.lost            "say_team $\{$_.r$stp_nick $_.r2[$_.r3%d$_.r2] lost&r} $_.str.dropped"
alias .tsay.safe            "say_team $\{$_.g$stp_nick $_.g2[$_.g3%l$_.g2] safe&r} $_.str.rlg"
alias .tsay.help            "say_team $\{$_.y$stp_nick $_.y2[$_.y3%l$_.y2] help&r} %A{&cfff%a&r}/{&cfff%h&r}"
alias .tsay.replace         "say_team $\{$_.c$stp_nick $_.c2[$_.c3%l$_.c2] replace&r} "
alias .tsay.coming          "say_team $\{$_.u$stp_nick $_.u2[$_.u3%l$_.u2] coming&r} %A{$_.u2%a/%h&r} $_.str.rlg"

alias .tsay.dm2.trick       "say_team $\{$_.c$stp_nick trick $_.c2[$_.c3%l$_.c2]&r}"
alias .tsay.dm2.slipped     "say_team $\{$_.r2enemy slipped&r}"
alias .tsay.dm2.rjump       "say_team $\{$_.u$stp_nick $_.u2[$_.u3%l$_.u2] coming rjump high&r}"
alias .tsay.dm2.secret      "say_team $\{$_.u$stp_nick $_.u2[$_.u3%l$_.u2] do not enter&r} %A{$_.u2%a/%h&r} $_.str.rlg"
alias .tsay.dm3.diss        "say_team $\{$_.b$stp_nick $_.b2[$_.b3%l$_.b2] discharge warning!&r} {&c29flg:$cells&r}"

alias .tsay.weapon.killed   "say_team $\{$_.g$stp_nick weapon killed! $_.g2[$_.g3%l$_.g2]&r} {\}{&c0ff0&cfff}{/}"

alias echo_tecken50         "echo 50:$x50 51:$x51 52:$x52 53:$x53 54:$x54 55:$x55 56:$x56 57:$x57 58:$x58 59:$x59"
alias echo_tecken60         "echo 60:$x60 61:$x61 62:$x62 63:$x63 64:$x64 65:$x65 66:$x66 67:$x67 68:$x68 69:$x69"
alias echo_tecken70         "echo 70:$x70 71:$x71 72:$x72 73:$x73 74:$x74 75:$x75 76:$x76 77:$x77 78:$x78 79:$x79"
alias echo_tecken80         "echo 80:$x80 81:$x81 82:$x82 83:$x83 84:$x84 85:$x85 86:$x86 87:$x87 88:$x88 89:$x89"
alias echo_tecken90         "echo 90:$x90 91:$x91 92:$x92 93:$x93 94:$x94 95:$x95 96:$x96 97:$x97 98:$x98 99:$x99"
alias echo_tecken           "echo_tecken50; echo_tecken60; echo_tecken70; echo_tecken80; echo_tecken90"


// Bind Aliases
////////////////////////////////////////////////////////////////////////////////////////////////////
alias stp_aol           "if ($health > 0) then %1 %2 %3 %4 %5 else stp_lost"
alias stp_spec          "if ('$spectator' == '1') then echo $_.wSudden teamplay message supressed when spectator = 1. else %1 %2 %3 %4 %5"

alias stp_report        "_.report; wait; .tsay.report"
alias stp_point         "_.point; wait; .tsay.point"
alias stp_took          "_.took"
alias stp_killme        "_.killme; wait; .tsay.killme"
alias stp_lost          "_.lost; wait; .tsay.lost"
alias stp_safe          "_.check_weapons; wait; .tsay.safe"
alias stp_powerup       "_.team_powerup"
alias stp_smart         "_.check_weapons; wait; _.smart"

alias stp_help          ".tsay.help"
alias stp_replace       ".tsay.replace"
alias stp_coming        "_.check_weapons; wait; .tsay.coming"
alias stp_wpkilled      ".tsay.weapon.killed"

alias stp_enemy_quad    ".tsay.enemy.quad"
alias stp_enemy_pent    ".tsay.enemy.pent"
alias stp_enemy_ring    ".tsay.enemy.ring"