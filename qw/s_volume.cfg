set _.vol.max 0.1
set _.vol.steps 10
set _.vol.s 3
set _.vol.str ""
set _.vol.p 0

// Calculate defaults
set_calc _.vol.min $_.vol.max / $_.vol.steps
set_calc _.vol.default $_.vol.min * $_.vol.s
set_calc _.vol.low $_.vol.steps / 3
set_calc _.vol.mid $_.vol.low * 2
vol $_.vol.default

// 80 (
// 81 ===
// 82 )
// 83 =*=
// Aliases
alias vol "set temp %1; _.vol.clamp temp 0 $_.vol.max; volume $temp; _.vol.set0"
alias vols "set _.vol.s %1; _.vol.clamp _.vol.s 0 $_.vol.steps; set_calc temp $_.vol.s * $_.vol.min; vol $temp"
alias volm "if ($_.vol.s > 0) then volmp else vols $_.vol.p"
alias volmp "set _.vol.p $_.vol.s; vols 0"
alias vol+ "inc _.vol.s +1; vols $_.vol.s"
alias vol- "inc _.vol.s -1; vols $_.vol.s"

alias _.vol.clamp   "if ($%1 < %2) then set %1 %2 else if ($%1 > %3) then set %1 %3"
alias _.vol.app     "set _.vol.str $_.vol.str%1"
alias _.vol.set0    "set _.vol.str $qt$qt; set _.vol.n 0; _.vol.set1"
alias _.vol.set1    "inc _.vol.n +1; if ($_.vol.n == $_.vol.s) then _.vol.app $x83 else _.vol.app $x81; _.vol.set1a"
alias _.vol.set1a   "if ($_.vol.n < $_.vol.steps) then _.vol.set1 else _.vol.set2"
alias _.vol.set2    "set _.vol.str vol$x20$x80$_.vol.str$x82$x20; if ($volume == 0) then _.vol.app m else _.vol.app $volume; _.vol.set3"
alias _.vol.set3    "if ($volume == 0) then set _.vol.clr &c855 else set _.vol.clr &cfff; _.vol.set4"
alias _.vol.set4    "set_calc _.vol.nl $con_notifylines - 1; _.vol.set4a"
alias _.vol.set4a   "if ($_.vol.nl > 0) then _.vol.set4b else _.vol.echo"
alias _.vol.set4b   "echo ; inc _.vol.nl -1; _.vol.set4a"
alias _.vol.echo    "echo $_.vol.clr$_.vol.str"
